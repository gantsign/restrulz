/*
 * Copyright 2016 GantSign Ltd. All Rights Reserved.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
grammar com.gantsign.restrulz.Restdsl with org.eclipse.xtext.common.Terminals

generate restdsl "http://www.gantsign.com/restrulz/Restdsl"

Model:
	simpleTypes+=SimpleType*
	classTypes+=ClassType*
	pathScopes+=PathScope*;

SimpleType:
	'type' name=ID ':' restriction=TypeRestriction;

terminal ID : ('a'..'z'|'A'..'Z'|'-'|'_')+;

TypeRestriction:
	StringRestriction;

terminal PATTERN : '^'('\\$' | !('\\$' | '$'))*'$';

StringRestriction:
	'string' pattern = PATTERN 'length' length=StringLengthRange;

StringLengthRange:
	range=IntRange;

IntRange:
	'[' start=INT '..' end=INT ']';

ClassType:
	'class' name=ID '{'
		properties+=Property*
	'}';

Property:
	name=ID (':' type=[Type])?;

Type:
	SimpleType | ClassType;

PathScope:
	'path' path=Path ':' name=ID '{'
		mappings+=RequestMapping*
	'}';

Path:
	elements+=DelimitedPathElement+;

DelimitedPathElement:
	'/' element=PathElement;

PathElement:
	StaticPathElement | PathParam;

StaticPathElement:
	value=ID;

PathParam:
	'{' name=ID (':' type=[Type])?'}';

RequestMapping:
	mapping=RequestHandler;

RequestHandler:
	handler=HttpMethod;

HttpMethod:
	name = ('GET' | 'PUT' | 'POST' | 'DELETE');
