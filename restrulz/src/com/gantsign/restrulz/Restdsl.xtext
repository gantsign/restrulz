/*
 * Copyright 2016 GantSign Ltd. All Rights Reserved.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
grammar com.gantsign.restrulz.Restdsl with org.eclipse.xtext.common.Terminals

generate restdsl "http://www.gantsign.com/restrulz/Restdsl"

Model:
	simpleTypes+=SimpleType*
	classTypes+=ClassType*
	responses+=Response*
	pathScopes+=PathScope*;

SimpleType:
	'type' name=ID ':' restriction=TypeRestriction;

terminal ID : ('a'..'z'|'A'..'Z'|'-'|'_')+;

TypeRestriction:
	StringRestriction;

terminal PATTERN : '^'('\\$' | !('\\$' | '$'))*'$';

StringRestriction:
	'string' pattern = PATTERN 'length' length=StringLengthRange;

StringLengthRange:
	'[' start=INT '..' end=INT ']';

ClassType:
	'class' name=ID '{'
		properties+=Property*
	'}';

Property:
	name=ID (':' type=[Type])?;

Type:
	SimpleType | ClassType;

PathScope:
	'path' ('/' path+=PathElement)+ ':' name=ID '{'
		mappings+=RequestMapping*
	'}';

PathElement:
	StaticPathElement | PathParam;

StaticPathElement:
	value=ID;

PathParam:
	'{' name=ID (':' type=[Type])?'}';

RequestMapping:
	RequestHandler;

RequestHandler:
	method=HttpMethod '->' name=ID
	'(' (parameters+=MethodParameter (',' parameters+=MethodParameter)*)? ')'
	':' response=[Response];

enum HttpMethod:
	GET = 'get' | PUT = 'put' | POST = 'post' | DELETE = 'delete';

MethodParameter:
	PathParamRef | BodyTypeRef;

PathParamRef:
	'/' ref=[PathParam];

BodyTypeRef:
	'*' ref=[ClassType];

Response:
	'response' name=ID ':' detail=ResponseWithBody;

ResponseWithBody:
	status=SuccessWithBodyStatus body=[ClassType];

enum SuccessWithBodyStatus:
	HTTP_200 = 'ok' | HTTP_201 = 'created' | HTTP_202 = 'accepted' | HTTP_206 = 'partial-content';

